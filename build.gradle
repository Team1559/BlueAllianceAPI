plugins {
  id 'java-library'
  id 'maven-publish'
  id 'signing'
}

version = '2024.0.1'

repositories {
  mavenCentral()
}

dependencies {
  implementation 'com.squareup.okhttp3:okhttp:4.12.0'
  implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.1'

  testImplementation 'org.junit.jupiter:junit-jupiter:5.10.3'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

test {
  useJUnitPlatform()
}

javadoc {
  options.addBooleanOption('html5', true)
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allJava
  archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
  dependsOn javadoc
  from javadoc.destinationDir
  archiveClassifier = 'javadoc'
}

publishing {
  publications {
    mavenCentral(MavenPublication) {
      groupId = 'org.victorrobotics.bluealliance'
      artifactId = 'blue-alliance-api'
      version = project.version

      from components.java
      artifact sourcesJar
      artifact javadocJar

      versionMapping {
        usage('java-api') {
          fromResolutionOf('runtimeClasspath')
        }
        usage('java-runtime') {
          fromResolutionResult()
        }
      }

      pom {
        name = 'Blue Alliance API'
        description = 'Access FRC event data from The Blue Alliance'
        url = 'https://github.com/Team1559/BlueAllianceAPI'
        inceptionYear = '2023'

        licenses {
          license {
            name = 'MIT License'
            url = 'https://opensource.org/license/mit'
          }
        }

        organization {
          name = 'Victor Robotics - FRC Team 1559'
          url = 'https://victorrobotics.org'
        }

        developers {
          developer {
            name = 'Alexander Bhalla'
            email = 'xander.bhalla@gmail.com'
            url = 'https://github.com/MathNerd28'
          }
        }

        scm {
          connection = 'scm:git:git://github.com/Team1559/BlueAllianceAPI.git'
          developerConnection = 'scm:git:ssh://github.com:Team1559/BlueAllianceAPI.git'
          url = 'https://github.com/Team1559/BlueAllianceAPI'
        }
      }
    }
  }

  repositories {
    maven {
      name = 'OSSRH'
      url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
      credentials {
        username = System.getenv('DEVIL_TECH_PUBLISH_USERNAME')
        password = System.getenv('DEVIL_TECH_PUBLISH_PASSWORD')
      }
    }
  }
}

signing {
  useGpgCmd()
  sign publishing.publications.mavenCentral
}
